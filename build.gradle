buildscript {
    dependencies {
        classpath 'de.guntram.mcmod:crowdin-translate:1.2'
    }
    repositories {
        maven {
            name = 'CrowdinTranslate source'
            url = "https://minecraft.guntram.de/maven/"
        }
    }
}

plugins {
	id 'fabric-loom' version '0.4-SNAPSHOT'
	id "com.modrinth.minotaur" version "1.1.0"
}

apply plugin: 'de.guntram.mcmod.crowdin-translate'
crowdintranslate.crowdinProjectName = 'antighost'
crowdintranslate.minecraftProjectName = 'antighost'
crowdintranslate.verbose = false

repositories {
	maven {
		url = "https://maven.fabricmc.net"
	}
	maven {
		url = "https://minecraft.guntram.de/maven/"
	}
	maven {
        name = 'CottonMC'
	    url = 'http://server.bbkr.space:8081/artifactory/libs-release'
	}    
}

sourceCompatibility = 1.8
targetCompatibility = 1.8

ext.Versions = new Properties()
Versions.load(file("Versionfiles/mcversion-1.16.4.properties").newReader())

archivesBaseName = "antighost"
ext.projectVersion = "1.1.3"

version = "${Versions['minecraft_version']}-fabric${Versions['fabric_versiononly']}-${project.projectVersion}"

minecraft {
    refmapName = "AntiGhost-refmap.json";
}

processResources {
	inputs.property "version", project.version

    from(sourceSets.main.resources.srcDirs) {
		include "fabric.mod.json"
		expand "version": project.version
	}
	from(sourceSets.main.resources.srcDirs) {
		exclude "fabric.mod.json"
	}
}

dependencies {
    minecraft  "com.mojang:minecraft:${Versions['minecraft_version']}"
    mappings   "net.fabricmc:yarn:${Versions['yarn_mappings']}:v2"
    modImplementation "net.fabricmc:fabric-loader:${Versions['loader_version']}"
    modImplementation "net.fabricmc.fabric-api:fabric-api:${Versions['fabric_version']}"
    modImplementation "io.github.cottonmc:cotton-client-commands:${Versions['ccc_version']}"
    include    "io.github.cottonmc:cotton-client-commands:${Versions['ccc_version']}"
    modImplementation "de.guntram.mcmod:crowdin-translate:1.2"
    include "de.guntram.mcmod:crowdin-translate:1.2"
}

// Loom will automatically attach sourcesJar to a RemapSourcesJar task and to the "build" task
// if it is present.
// If you remove this task, sources will not be generated.
task sourcesJar(type: Jar, dependsOn: classes) {
	classifier = 'sources'
	from sourceSets.main.allSource
}

jar {
	from "LICENSE"
}

build {
        dependsOn downloadTranslations
}

import com.modrinth.minotaur.TaskModrinthUpload

task publishModrinth (type: TaskModrinthUpload){

    token = System.getenv("MODRINTH_TOKEN")
    projectId = 'Jw3Wx1KR'
    versionNumber = project.version
    versionName = 'antighost'
    releaseType = 'release'
    uploadFile = remapJar // This is the java jar task
    addGameVersion("${Versions['minecraft_version']}")
    addLoader('fabric')
}
